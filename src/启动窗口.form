覆写方法 正在载入()
   父对象.正在载入()
   启动窗口.显示标题栏 = 假
   共享数据.初始化数据(本对象,"主题真假")
   变量 主题逻辑 为 逻辑型 = 共享数据.取逻辑("主题真假")
   如果 主题逻辑 == 假 则
      启动窗口.主题 = R.style.liang
      状态栏工具类.置状态栏颜色及透明度(本对象,"#FFFFFFFF",0)
      启动窗口.状态栏字体黑色 = 真
   否则
      启动窗口.主题 = R.style.an
      状态栏工具类.置状态栏颜色及透明度(本对象,"#FF212121",0)
      启动窗口.状态栏字体黑色 = 假
   结束 如果
结束 方法

覆写方法 载入布局完毕()
   父对象.载入布局完毕()
   权限申请1.申请权限("android.permission.READ_EXTERNAL_STORAGE","android.permission.WRITE_EXTERNAL_STORAGE")
   变量 主题逻辑 为 逻辑型 = 共享数据.取逻辑("主题真假")
   如果 主题逻辑 == 假 则
      相对布局1.背景颜色 = "#FFFFFFFF"
      文本框1.字体颜色 = 黑色
   否则
      相对布局1.背景颜色 = "#FF212121"
      文本框1.字体颜色 = 白色
   结束 如果
结束 方法

事件 启动窗口:权限申请回调(请求码 为 整数型,权限列表 为 文本型[],请求结果 为 整数型[])
   变量 是否有存储权限 为 逻辑型 = 权限申请1.是否缺少权限("android.permission.READ_EXTERNAL_STORAGE","android.permission.WRITE_EXTERNAL_STORAGE")
   如果 是否有存储权限 == 真 则
      //缺少
      变量 对话框1 为 对话框 = 创建 对话框(本对象)
      对话框1.标题 = "存储权限申请"
      对话框1.信息 = "软件必须拥有存储权限才能使用"
      对话框1.按钮1 = "给予"
      对话框1.按钮2 = "取消"
      对话框1.可取消 = 假
      对话框1.显示()
      事件 对话框1:按钮1被单击()
         权限申请1.申请权限("android.permission.READ_EXTERNAL_STORAGE","android.permission.WRITE_EXTERNAL_STORAGE")
      结束 事件
      事件 对话框1:按钮2被单击()
         对话框1.关闭()
         关闭程序()
      结束 事件
   否则
      //拥有
      倒计时进入主界面.开始倒计时(2000,1000)
      事件 倒计时进入主界面:倒计时结束()
         启动窗口.切换窗口(创建 主界面(),淡入淡出)
         倒计时进入主界面.关闭()
         关闭窗口()
      结束 事件
   结束 如果
结束 事件

事件 文本框2:被单击()
   启动窗口.切换窗口(创建 主界面(),淡入淡出)
   倒计时进入主界面.关闭()
   关闭窗口()
结束 事件

事件 文本框3:被单击()
   启动窗口.切换窗口(创建 主界面(),淡入淡出)
   倒计时进入主界面.关闭()
   关闭窗口()
结束 事件